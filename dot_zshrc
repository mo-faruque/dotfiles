# Auto-attach to tmux session (must be before anything else)
# Uses exec so detach/exit will close the terminal
if command -v tmux &> /dev/null && [ -z "$TMUX" ] && [[ $- == *i* ]]; then
    exec tmux new-session -A -s default
fi

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
typeset -g POWERLEVEL9K_INSTANT_PROMPT=quiet
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### Zinit Setup ###
source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"

# Load Powerlevel10k theme (not deferred - needed for instant prompt)
zinit ice depth=1
zinit light romkatv/powerlevel10k

# Essential plugins with Turbo mode (deferred loading for max speed)
zinit wait"0a" lucid for \
    atinit"zicompinit; zicdreplay" \
        zdharma-continuum/fast-syntax-highlighting \
    atload"_zsh_autosuggest_start" \
        zsh-users/zsh-autosuggestions \
    blockf atpull'zinit creinstall -q .' \
        zsh-users/zsh-completions

# Git plugin (OMZ) with turbo - load later
zinit wait"0b" lucid for \
    OMZL::git.zsh \
    OMZP::git

# Autosuggest settings (async + optimized)
export ZSH_AUTOSUGGEST_USE_ASYNC=1
export ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=20
export ZSH_AUTOSUGGEST_MANUAL_REBIND=1
export ZSH_AUTOSUGGEST_STRATEGY=(history completion)

### User Configuration ###

# Set Neovim as default editor
export EDITOR='nvim'
export VISUAL='nvim'

# Add paths (consolidated)
export PATH="$HOME/.local/bin:$HOME/btop/bin:$HOME/bin:/usr/lib/wsl/lib:/usr/local/bin:/usr/bin:$PATH"

# Ruby gems
export GEM_HOME=$HOME/.local/share/gem/ruby/3.0.0
export PATH=$HOME/.local/share/gem/ruby/3.0.0/bin:$PATH

# Aliases
alias cls='clear'

# eza (modern ls)
if command -v eza &> /dev/null; then
    alias ls='eza'
    alias ll='eza -l --icons --git'
    alias la='eza -la --icons --git'
    alias lt='eza --tree --level=2 --icons'
    alias l='eza -la --icons --git'
fi

# bat (modern cat)
if command -v bat &> /dev/null; then
    alias cat='bat --paging=never'
    alias catp='bat'  # with pager
fi

# lazygit
alias lg='lazygit'

# tlrc alias (in case installed as tlrc instead of tldr)
command -v tlrc &> /dev/null && ! command -v tldr &> /dev/null && alias tldr='tlrc'

# yazi file manager (cd to dir on exit)
function yy() {
    local tmp="$(mktemp -t "yazi-cwd.XXXXXX")"
    yazi "$@" --cwd-file="$tmp"
    if cwd="$(cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
        cd -- "$cwd"
    fi
    rm -f -- "$tmp"
}

# Lazy load NVM (huge speed boost - NVM is slow)
export NVM_DIR="$HOME/.nvm"
__load_nvm() {
  unset -f nvm node npm npx claude __load_nvm
  [ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"
  [ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"
  nvm use 18 --silent >/dev/null
}
nvm() { __load_nvm; nvm "$@"; }
node() { __load_nvm; node "$@"; }
npm() { __load_nvm; npm "$@"; }
npx() { __load_nvm; npx "$@"; }
claude() { __load_nvm; "$HOME/.claude/local/claude" "$@"; }

# Lazy load Conda (also slow)
__conda_setup() {
  unset -f conda mamba
  export PATH="$HOME/anaconda3/bin:$PATH"
  source "$HOME/anaconda3/etc/profile.d/conda.sh"
  conda activate test
}
conda() { __conda_setup; conda "$@"; }
mamba() { __conda_setup; mamba "$@"; }
# Auto-activate if in conda project
[[ -f "environment.yml" || -f "conda.yaml" ]] && __conda_setup

# Add atuin to PATH (installed to ~/.atuin/bin by official installer)
[[ -d "$HOME/.atuin/bin" ]] && export PATH="$HOME/.atuin/bin:$PATH"

# Defer zoxide and atuin init
zinit wait"0c" lucid for \
    atload'eval "$(zoxide init zsh)"' \
        zdharma-continuum/null \
    atload'command -v atuin &>/dev/null && eval "$(atuin init zsh)"' \
        zdharma-continuum/null

# VS Code shell integration (only when needed)
[[ "$TERM_PROGRAM" == "vscode" ]] && . "$(code --locate-shell-integration-path zsh)"

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

